buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.2'
    }
}

apply plugin: 'com.android.application'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    google()
}

apply plugin: 'io.fabric'

android {
    compileSdkVersion 26

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.appName}-${variant.name}-${variant.versionName}-${new Date().format('ddMMyyyy')}.apk"
        }
    }

    defaultConfig {
        applicationId "com.jakebarnby.filemanager"
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

configurations {
    implementation.exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })
    testImplementation 'junit:junit:4.12'

    //Android
    implementation 'com.android.support:multidex:1.0.2'
    implementation "com.android.support:appcompat-v7:${project.supportLibVersion}"
    implementation "com.android.support:design:${project.supportLibVersion}"
    implementation "com.android.support:cardview-v7:${project.supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${project.supportLibVersion}"
    implementation "com.android.support:percent:${project.supportLibVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta3'

    //Dropbox
    implementation 'com.dropbox.core:dropbox-core-sdk:3.0.3'
    implementation 'com.squareup.okhttp3:okhttp:3.8.0'

    //Firebase
    implementation "com.google.android.gms:play-services-auth:${project.playServicesVersion}"
    implementation "com.google.firebase:firebase-core:${project.playServicesVersion}"
    implementation "com.google.firebase:firebase-messaging:${project.playServicesVersion}"
    implementation "com.google.firebase:firebase-config:${project.playServicesVersion}"
    implementation "com.google.android.gms:play-services-ads:${project.playServicesVersion}"

    //Google Drive
    implementation "com.google.api-client:google-api-client-android:${project.googleApiVersion}"
    implementation "com.google.apis:google-api-services-drive:v3-rev75-${project.googleApiVersion}"

    //OneDrive
    implementation project(':msal')
    implementation 'com.microsoft.graph:msgraph-sdk-android:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.1'

    //FAB Menu Button
    implementation 'io.github.yavski:fab-speed-dial:1.0.6'

    //Screen blur
    implementation 'jp.wasabeef:blurry:2.1.1'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.1.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.1.1'
    implementation 'com.github.bumptech.glide:recyclerview-integration:4.1.1@aar'

    //CirclePercentProgressBar
    implementation 'com.dinuscxj:circleprogressbar:1.1.1'

    //Play services billing
    implementation 'com.android.billingclient:billing:1.0'

    //Tutorial
    implementation project(':tutorial')

    //Crashlytics
    api('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'
