apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 29

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.name}-${variant.name}-${variant.versionName}-${new Date().format('ddMMyyyy')}.apk"
        }
    }

    defaultConfig {
        applicationId "com.jakebarnby.filemanager"
        minSdkVersion 18
        targetSdkVersion 29
        versionCode 3
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
            }
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

configurations {
    implementation.exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.core:core-ktx:1.2.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5'

    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })
    testImplementation 'junit:junit:4.12'

    //Android
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    //Dropbox
    implementation 'com.dropbox.core:dropbox-core-sdk:3.0.3'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'

    //Firebase
    implementation "com.google.android.gms:play-services-auth:18.0.0"
    implementation "com.google.firebase:firebase-core:17.3.0"
    implementation 'com.google.firebase:firebase-analytics:17.3.0'
    implementation "com.google.firebase:firebase-messaging:20.1.5"
    implementation "com.google.firebase:firebase-config:19.1.3"
    implementation "com.google.android.gms:play-services-ads:19.0.1"

    //Google Drive
    implementation "com.google.api-client:google-api-client-android:1.22.0"
    implementation "com.google.apis:google-api-services-drive:v3-rev75-1.22.0"

    //OneDrive
    implementation project(':msal')
    implementation 'com.microsoft.graph:msgraph-sdk-android:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.5'

    //FAB Menu Button
    implementation 'io.github.yavski:fab-speed-dial:1.0.6'

    //Screen blur
    implementation 'jp.wasabeef:blurry:2.1.1'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.1.1'
    kapt 'com.github.bumptech.glide:compiler:4.1.1'
    implementation 'com.github.bumptech.glide:recyclerview-integration:4.1.1@aar'

    //CirclePercentProgressBar
    implementation 'com.dinuscxj:circleprogressbar:1.1.1'

    //Play services billing
    implementation 'com.android.billingclient:billing:2.2.0'
    implementation 'com.android.billingclient:billing-ktx:2.2.0'

    //Tutorial
    implementation project(':tutorial')

    //Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0-beta04'
    implementation 'com.google.firebase:firebase-crashlytics-ndk:17.0.0-beta04'

    // Wtf?
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
}

apply plugin: 'com.google.gms.google-services'
